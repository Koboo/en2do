import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id('java-library')
    id('com.gradleup.shadow') version('8.3.6')
    id('com.vanniktech.maven.publish') version('0.29.0')
}

group('eu.koboo')
version('3.2.0')

repositories {
    mavenCentral()
}

dependencies {

    def mongoDriverVersion = "5.5.1"
    // The driver itself
    api("org.mongodb:mongodb-driver-sync:$mongoDriverVersion")
    testImplementation("org.mongodb:mongodb-driver-sync:$mongoDriverVersion")

    // Eliminate boilerplate code
    def lombokVersion = "1.18.38"
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    testImplementation("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")

    // Test-Suite runtime
    def jupiterVersion = "5.13.1"
    testImplementation("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-api:$jupiterVersion")
    testImplementation("org.junit.platform:junit-platform-launcher:1.13.1")

    // SLF4j logger for test-suite
    def slf4jVersion = "2.0.17"
    testImplementation("org.slf4j:slf4j-jdk14:$slf4jVersion")
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    toolchain.languageVersion.set(JavaLanguageVersion.of(11))
}

compileJava {
    options.fork = true
    options.encoding = 'UTF-8'
    options.release.set(11)
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}
tasks.register('javadocJar', Jar) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

artifacts {
    archives(shadowJar)
    archives(sourcesJar)
    archives(javadocJar)
}

mavenPublishing {
    coordinates(project.group.toString(), "${project.name}", project.version.toString())

    pom {
        name = 'En2do'
        description = 'MongoDB entity to document wrapper'
        inceptionYear = '2022'
        url = 'https://github.com/Koboo/en2do'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'koboo'
                name = 'Koboo'
                email = 'admin@koboo.eu'
                url = 'https://github.com/Koboo/'
            }
        }
        scm {
            url = 'https://github.com/Koboo/en2do'
            connection = 'scm:git:https://github.com/Koboo/en2do'
            developerConnection = 'scm:git:https://github.com/Koboo/en2do'
        }
    }

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
}